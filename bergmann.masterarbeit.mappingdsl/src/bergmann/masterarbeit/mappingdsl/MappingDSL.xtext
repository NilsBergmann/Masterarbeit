grammar bergmann.masterarbeit.mappingdsl.MappingDSL with org.eclipse.xtext.common.Terminals

generate mappingDSL "http://www.masterarbeit.bergmann/mappingdsl/MappingDSL"

Domain:
	elems+=DomainElement*
;

DomainElement:
	DomainDataType | DomainValue
;

DomainDataType:
	{DomainDataType}
	"datatype:" "{"
	 	(
	 		("name:" name=ID) &
	 		(attributes+=DataTypeAttribute* )
	 	)
	"}"
;
DataTypeAttribute:
	{DomainTypeAttribute}
	"attribute:" "{"
	(
		("name:" name=ID) &
		("type:" type=VALUETYPE) &
		("unit:" unit=Unit)? &
		("description:" description=STRING)? &
		("source:" source=DataSource)? 
	)
	"}"
;

DomainValue:
	{DomainValue}
	"value:" "{"
	(
		("name:" name=ID) &
		("type:" type=VALUETYPE) &
		("unit:" unit=Unit)? &
		("description:" description=STRING)? &
		("source:" source=DataSource)? 
	)
	"}"
;

DataSource:
	DataSourceJavaReference | DataSourceDatabaseValue
;

DataSourceJavaReference returns DataSource:
	{JavaDataSource}
	"WIP_DataSourceJavaReference"
;

DataSourceDatabaseValue returns DataSource:
	{DatabaseDataSource}
	"database:" "{"
		"columnLabel:" columnLabel = STRING
	"}"
;

enum VALUETYPE:
	NUMBER = "number" | STRING = "string" | BOOLEAN = "boolean"
;

Unit:
 	UnitDivision
;

UnitDivision returns Unit:
	UnitMultiplication ({UnitDiv.left=current} "/" right=UnitMultiplication)*
;

UnitMultiplication returns Unit:
	UnitExponent ({UnitMult.left=current} "*" right=UnitExponent)*
;

UnitExponent returns Unit:
	{UnitExponent} (value=UnitAtom '^' negative?='-' exponent=INT)
	| UnitAtom
;

UnitAtom returns Unit:
	{UnitBraces} '(' unit=Unit ')'
	| {UnitLiteralAtom} unit=(LengthUnitLiteral  | TimeUnitLiteral)
;


LengthUnitLiteral returns UnitLiteral:
	{LengthUnitLiteral} unit=LENGTH_UNIT
;
TimeUnitLiteral returns UnitLiteral:
	{TimeUnitLiteral} unit=TIME_UNIT
;

terminal TIME_UNIT:
	"ms" | "s"| "min" | "h"
;

terminal LENGTH_UNIT:
	'mm' | 'cm' | 'm' | 'km'
;
