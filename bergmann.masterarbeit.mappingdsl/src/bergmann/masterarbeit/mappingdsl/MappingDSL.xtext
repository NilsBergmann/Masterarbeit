grammar bergmann.masterarbeit.mappingdsl.MappingDSL with org.eclipse.xtext.xbase.Xtype
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

generate mappingDSL "http://www.masterarbeit.bergmann/mappingdsl/MappingDSL"

Domain:
	elems+=DomainElement*
;
DomainElement:
	JavaDataType | DomainValue | CustomJava
;

JavaDataType:
	{DataType}
	"datatype:" "{"
	(
		("name:" name=ID ) &
		("javaClass:" javaType=[jvmTypes::JvmType|QualifiedName])
	)
"}"
;

CustomJava:
	LiteralJava | UnaryJava | BinaryJava
;

LiteralJava returns CustomJava:
	{LiteralJava}
	"java" "{"
	(
		("name:" name=ID) &
		("type:" type=Type) &
		("description:" description=STRING)? &
		("javaClass:" javaType=[jvmTypes::JvmType|QualifiedName])
	)
	"}"
;
UnaryJava returns CustomJava:
	{UnaryJava}
	"java" "{"
	(
		("name:" name=ID) &
		("type:" type1=Type "->" type2=Type) &
		("description:" description=STRING)? &
		("javaClass:" javaType=[jvmTypes::JvmType|QualifiedName])
	)
	"}"
;

BinaryJava returns CustomJava:
	{BinaryJava}
	"java" "{"
	(
		("name:" name=ID) &
		("type:" type1=Type "->" type2=Type "->" type3=Type) &
		("description:" description=STRING)? &
		("javaClass:" javaType=[jvmTypes::JvmType|QualifiedName])
	)
	"}"
;
DomainValue:
	{DomainValue}
	"databaseValue:" "{"
	(
		("name:" name=ID) &
		("type:" type=BASE_VALUETYPE) &
		("unit:" unit=Unit)? &
		("description:" description=STRING)? &
		("column:" column=STRING) 
	)
	"}"
;

Type:
	{JavaType} type=[JavaDataType] | {BaseType} type=BASE_VALUETYPE
;
enum BASE_VALUETYPE:
	NUMBER = "number" | STRING = "string" | BOOLEAN = "boolean"
;

Unit:
 	UnitDivision
;

UnitDivision returns Unit:
	UnitMultiplication ({UnitDiv.left=current} "/" right=UnitMultiplication)*
;

UnitMultiplication returns Unit:
	UnitExponent ({UnitMult.left=current} "*" right=UnitExponent)*
;

UnitExponent returns Unit:
	{UnitExponent} (value=UnitAtom '^' negative?='-' exponent=INT)
	| UnitAtom
;

UnitAtom returns Unit:
	{UnitBraces} '(' unit=Unit ')'
	| {UnitLiteralAtom} unit=(LengthUnitLiteral  | TimeUnitLiteral)
;


LengthUnitLiteral returns UnitLiteral:
	{LengthUnitLiteral} unit=LENGTH_UNIT
;
TimeUnitLiteral returns UnitLiteral:
	{TimeUnitLiteral} unit=TIME_UNIT
;

terminal TIME_UNIT:
	"ms" | "s"| "min" | "h"
;

terminal LENGTH_UNIT:
	'mm' | 'cm' | 'm' | 'km'
;

terminal INT returns ecore::EInt: ('0'..'9')+;
